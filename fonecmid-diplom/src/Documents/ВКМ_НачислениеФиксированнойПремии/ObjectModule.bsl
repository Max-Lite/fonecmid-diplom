
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ОбработчикиСобытий

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	//Запись движений: Дополнительные начисления	
	Для Каждого Строка Из Начисления Цикл
		
		Движение = Движения.ВКМ_ДополнительныеНачисления.Добавить();
		Движение.ПериодРегистрации = Дата;
		Движение.ВидРасчета = ПланыВидовРасчета.ВКМ_ДополнительныеНачисления.ФиксированнаяПремия;
		Движение.Сотрудник = Строка.Сотрудник;	
		Движение.БазовыйПериодНачало = НачалоМесяца(Дата);
		Движение.БазовыйПериодКонец = КонецМесяца(Дата);
		Движение.Сумма = Строка.СуммаПремии;		
	КонецЦикла;
		
	Движения.ВКМ_ДополнительныеНачисления.Записать();
		
	//Запись движений: Удержания
	Для Каждого Строка Из Начисления Цикл
	
		Движение = Движения.ВКМ_Удержания.Добавить();
		Движение.ПериодРегистрации = Дата;
		Движение.ВидРасчета = ПланыВидовРасчета.ВКМ_Удержания.НДФЛ;
		Движение.Сотрудник = Строка.Сотрудник;
		Движение.БазовыйПериодНачало = НачалоМесяца(Дата);
		Движение.БазовыйПериодКонец = КонецМесяца(Дата);
		
	КонецЦикла;
		
	Движения.ВКМ_Удержания.Записать();
	
	//Расчет движений: Удержания
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ВКМ_Удержания.НомерСтроки КАК НомерСтроки,
		|	ВКМ_УдержанияБазаВКМ_ДополнительныеНачисления.СуммаБаза КАК Сумма,
		|	Константы.ВКМ_НДФЛ КАК НДФЛ
		|ИЗ
		|	РегистрРасчета.ВКМ_Удержания КАК ВКМ_Удержания
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрРасчета.ВКМ_Удержания.БазаВКМ_ДополнительныеНачисления(&Измерения, &Измерения, &Разрезы,
		|			Регистратор = &Ссылка
		|		И ВидРасчета = &НДФЛ) КАК ВКМ_УдержанияБазаВКМ_ДополнительныеНачисления
		|		ПО ВКМ_Удержания.НомерСтроки = ВКМ_УдержанияБазаВКМ_ДополнительныеНачисления.НомерСтроки,
		|	Константы КАК Константы
		|ГДЕ
		|	ТИПЗНАЧЕНИЯ(ВКМ_УдержанияБазаВКМ_ДополнительныеНачисления.РегистраторРазрез) = ТИП(Документ.ВКМ_НачислениеФиксированнойПремии)
		|	И ВКМ_Удержания.Регистратор = &Ссылка
		|	И ВКМ_Удержания.ВидРасчета = &НДФЛ"; 
		
	Измерения = Новый Массив; 
	Измерения.Добавить("Сотрудник");
	
	Разрезы = Новый Массив;
	Разрезы.Добавить("Регистратор");
	
	Запрос.УстановитьПараметр("Измерения", Измерения);
	Запрос.УстановитьПараметр("Разрезы", Разрезы);
	Запрос.УстановитьПараметр("НДФЛ", ПланыВидовРасчета.ВКМ_Удержания.НДФЛ);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Движение = Движения.ВКМ_Удержания[Выборка.НомерСтроки - 1];
		Движение.Сумма = Выборка.Сумма * Выборка.НДФЛ / 100;
	КонецЦикла;
	
	Движения.ВКМ_Удержания.Записать(, Истина);
	
	// Создание движений прихода в регистре накопления ВзаиморасчетыССотрудниками 
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ВКМ_НачислениеФиксированнойПремииНачисления.Сотрудник КАК Сотрудник
		|ПОМЕСТИТЬ втСписокСотрудников
		|ИЗ
		|	Документ.ВКМ_НачислениеФиксированнойПремии.Начисления КАК ВКМ_НачислениеФиксированнойПремииНачисления
		|ГДЕ
		|	ВКМ_НачислениеФиксированнойПремииНачисления.Ссылка = &Ссылка
		|СГРУППИРОВАТЬ ПО
		|	ВКМ_НачислениеФиксированнойПремииНачисления.Сотрудник
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВКМ_ДополнительныеНачисления.Сотрудник КАК Сотрудник,
		|	ЕСТЬNULL(СУММА(ВКМ_ДополнительныеНачисления.Сумма), 0) КАК ДопНачисления,
		|	ЕСТЬNULL(СУММА(ВКМ_Удержания.Сумма), 0) КАК Удержания
		|ИЗ
		|	РегистрРасчета.ВКМ_ДополнительныеНачисления КАК ВКМ_ДополнительныеНачисления
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрРасчета.ВКМ_Удержания КАК ВКМ_Удержания
		|		ПО ВКМ_ДополнительныеНачисления.Сотрудник = ВКМ_Удержания.Сотрудник
		|ГДЕ
		|	ВКМ_ДополнительныеНачисления.Сотрудник В
		|		(ВЫБРАТЬ
		|			втСписокСотрудников.Сотрудник КАК Сотрудник
		|		ИЗ
		|			втСписокСотрудников КАК втСписокСотрудников)
		|	И ВКМ_ДополнительныеНачисления.Регистратор = &Ссылка
		|	И ВКМ_Удержания.Регистратор = &Ссылка
		|	И ВКМ_Удержания.Сотрудник В
		|		(ВЫБРАТЬ
		|			втСписокСотрудников.Сотрудник КАК Сотрудник
		|		ИЗ
		|			втСписокСотрудников КАК втСписокСотрудников)
		|СГРУППИРОВАТЬ ПО
		|	ВКМ_ДополнительныеНачисления.Сотрудник";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Выборка.Сбросить();
	
	Движения.ВКМ_ВзаиморасчетыССотрудниками.Записывать = Истина;

	Пока Выборка.Следующий() Цикл
		Движение = Движения.ВКМ_ВзаиморасчетыССотрудниками.ДобавитьПриход();
		ЗаполнитьЗначенияСвойств(Движение, Выборка);
		Движение.Период = Дата;
		Движение.Сумма = Выборка.ДопНачисления - Выборка.Удержания;
	КонецЦикла;
	
	Движения.ВКМ_ВзаиморасчетыССотрудниками.Записать();
КонецПроцедуры

#КонецОбласти

#КонецЕсли
